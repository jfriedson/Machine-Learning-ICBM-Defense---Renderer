#version 460

layout (triangles, equal_spacing, ccw) in;

in vec3 tc_position[];
in vec2 tc_texcoord[];
in vec3 tc_normal[];
in vec3 tc_tangent[];

out vec3 te_position;
out vec2 te_texcoord;
out vec3 te_normal;

out mat3 te_tangentSpace;
out vec4 te_fragPosLightSpace;

uniform mat4 ModelMatrix;
uniform mat4 ViewMatrix;
uniform mat4 ProjectionMatrix;
uniform mat4 lightSpaceMatrix;

uniform sampler2D dispTex;


vec2 interpolate2D(vec2 v0, vec2 v1, vec2 v2)
{
    return vec2(gl_TessCoord.x) * v0 + vec2(gl_TessCoord.y) * v1 + vec2(gl_TessCoord.z) * v2;
}

vec3 interpolate3D(vec3 v0, vec3 v1, vec3 v2)
{
    return vec3(gl_TessCoord.x) * v0 + vec3(gl_TessCoord.y) * v1 + vec3(gl_TessCoord.z) * v2;
}


void main()
{
    //vec3 normal = interpolate3D(tc_normal[0], tc_normal[1], tc_normal[2]);
    //te_normal = mat3(ModelMatrix) * normal;

    te_texcoord = interpolate2D(tc_texcoord[0], tc_texcoord[1], tc_texcoord[2]);
    
    vec3 position = interpolate3D(tc_position[0], tc_position[1], tc_position[2]);
    vec3 normal = normalize(position);
    te_normal = normalize(mat3(ModelMatrix) * normal);

    vec3 Tangent = normalize(tc_tangent[0] - dot(tc_tangent[0], normal) * normal);
    vec3 Bitangent = cross(Tangent, normal);

    Tangent = normalize(mat3(ModelMatrix) * Tangent);
    Bitangent = normalize(mat3(ModelMatrix) * Bitangent);
    te_tangentSpace = mat3(Tangent, Bitangent, te_normal);


    float radius_adj = 10.f - distance(position, vec3(0.f));
    float height = texture(dispTex, te_texcoord).r;
    position += normal * (height * 0.1f + radius_adj);
    te_position = mat3(ModelMatrix) * position;

    te_fragPosLightSpace = lightSpaceMatrix * vec4(te_position, 1.f);

    gl_Position = ProjectionMatrix * ViewMatrix * ModelMatrix * vec4(position.xyz, 1.f);
}
